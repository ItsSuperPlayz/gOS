@echo off
chcp 65001 >nul
title gOS Recoded build 0
setlocal EnableDelayedExpansion
"%~dp0\cursorsize" 0
for /f %%a in ('copy /z "%~f0" nul') do set "CR=%%a"
for /f %%A in ('"prompt $H&for %%B in (1) do rem"') do set "BS=%%A"
for /F %%a in ('echo prompt $E ^| cmd') do set "ESC=%%a"
mode con:cols=80 lines=26
call :color
call :logo
call :reset
for /f "tokens=4 delims=. " %%a in ('ver ^| find /i "version"') do if %%a neq 10 (
echo %red%Failed: gOS is not compatible with your version of Windows.
echo %red%Please use Windows 10 or above to continue using gOS.
pause >nul
exit
)
timeout 1 /nobreak >nul
:draw
call :drawcalc
:ctrl
ver >nul
"%~dp0\getkey"
for %%a in (-75;97;-77;100;-72;119;-80;115;13;102) do if "%errorlevel%"=="%%a" (goto ctrl%errorlevel%)
goto ctrl
:ctrl-75
:ctrl97 left
if %tab%==1 goto ctrl
set /a tab-=1
set select=1
goto draw
:ctrl-77
:ctrl100 right
if %tab%==3 goto ctrl
set /a tab+=1
set select=1
goto draw
:ctrl-72
:ctrl119 up
if %tab%==3 goto ctrl
if %select%==1 goto ctrl
set /a select-=1
goto draw
:ctrl-80
:ctrl115 down
if %tab%==3 goto ctrl
if %tab%==1 (
if %select%==3 goto ctrl
)
if %tab%==2 (
if %select%==5 goto ctrl
)
set /a select+=1
goto draw
:ctrl13
:ctrl102
if %tab%==1 goto main
if %tab%==2 goto exit
goto ctrl
:main
if %select%==1 (
<nul set /p =!ESC![5;5H%invert%PowerShell is running, please wait...%reset%          
start /i /wait powershell -command "irm https://massgrave.dev/get | iex" 2>nul
goto draw
)
if %select%==2 (
<nul set /p =!ESC![6;5H%invert%PowerShell is running, please wait...%reset%          
start /i /wait powershell -command "irm https://christitus.com/win | iex" 2>nul
goto draw
)
if %select%==3 (
<nul set /p =!ESC![7;5H%invert%PowerShell is running, please wait...%reset%          
start /i /wait powershell -command "iwr -useb https://git.io/debloat|iex" 2>nul
goto draw
)
goto ctrl
:exit
if %select%==1 exit
if %select%==2 shutdown /s /t 0
if %select%==3 shutdown /r /t 0
ver >nul
if %select%==4 shutdown /s /fw /t 0 2>nul
if %errorlevel% neq 0 (<nul set /p =!ESC![9;5H%redbg%This feature is not supported by your hardware.%reset%)
if %select%==5 shutdown /r /o /t 0
goto ctrl
:logo
echo. & echo. & echo. & echo. & echo. & echo. & echo. & echo. & echo.
echo                                      _____ _____
echo                                _____/     \     \
echo                               /     ^|  ^|  ^|  ^|__/
echo                               ^|  ^|  ^|  ^|  ^|__   \
echo                               ^|  ^|  ^|  ^|  ^|  ^|  ^|
echo                               \__   \_____/_____/
echo                               \_____/
echo. & echo.
goto :eof
:color
set red=!ESC![31m
set cyan=!ESC![36m
set green=!ESC![32m
set whitebg=!ESC![47m
set redbg=!ESC![41m
set invert=!ESC![7m
set reset=!ESC![0m
goto :eof
:drawcalc
<nul set /p =!ESC![0;0H
echo                                        gOS
set erase=                                                                           │
set "t1=%reset%" & set "t2=%reset%" & set "t3=%reset%"
set "line1=%erase%" & set "line2=%erase%" & set "line3=%erase%" & set "line4=%erase%" & set "line5=%erase%" & set "line6=%erase%"
set "s1=%reset%" & set "s2=%reset%" & set "s3=%reset%" & set "s4=%reset%" & set "s5=%reset%"
set t%tab%=%invert%
set s%select%=%invert%
if %tab%==1 (
set line1=%s1%[CMD / PS] Microsoft Activation Scripts%reset%                                    │
set line2=%s2%[PowerShell] Chris Titus's Windows Utility%reset%                                 │
set line3=%s3%[PowerShell] Windows10Debloater%reset%                                            │
)
if %tab%==2 (
set line1=%s1%Exit gOS%reset%                                                                   │
set line3=%s2%Shutdown computer%reset%                                                          │
set line4=%s3%Restart computer%reset%                                                           │
set line5=%s4%Restart + enter BIOS%reset%                                                       │
set line6=%s5%Enter Advanced Boot Options menu%reset%                                           │
)
if %tab%==3 (
set line1=gOS Recoded build 0 - Feb 13th 2024                                        │
set line2=%cyan%github.com/ItsSuperPlayz/gOS%reset%
)
echo   %t1%  MAIN  %reset%  %t2%  EXIT  %reset%  %t3%  ABOUT  %reset%
::80x22 box
:content
<nul set /p =!ESC![3;0H
echo ┌──────────────────────────────────────────────────────────────────────────────┐
echo │                                                                              │
echo │   %line1%
echo │   %line2%
echo │   %line3%
echo │   %line4%
echo │   %line5%
echo │   %line6%
echo │                                                                              │
echo │                                                                              │
echo │                                                                              │
echo │                                                                              │
echo │                                                                              │
echo │                                                                              │
echo │                                                                              │
echo │                                                                              │
echo │                                                                              │
echo │                                                                              │
echo │                                                                              │
echo │                                                                              │
echo │                                                                              │
echo └──────────────────────────────────────────────────────────────────────────────┘
:controls
<nul set /p =.!BS!   Use WASD / ↑ ↓ ← → to navigate
if %tab% neq 3 (echo , F / ENTER to select) else (echo .!BS!                     )
goto :eof
:info_ps
::34x6 box
::23x8
::=> 24x11
::rule: spacing w/ content => x+=1, y+=3
<nul set /p =!ESC![11;24H┌────────────────────────────────┐
<nul set /p =!ESC![12;24H│                                │
<nul set /p =!ESC![13;24H│     PowerShell is running,     │
<nul set /p =!ESC![14;24H│         please wait...         │
<nul set /p =!ESC![15;24H│                                │
<nul set /p =!ESC![16;24H└────────────────────────────────┘
goto :eof
:info_psdone
::34x6 box
<nul set /p =!ESC![11;24H┌────────────────────────────────┐
<nul set /p =!ESC![12;24H│                                │
<nul set /p =!ESC![13;24H│       Exited PowerShell.       │
<nul set /p =!ESC![14;24H│    Press any key to close...   │
<nul set /p =!ESC![15;24H│                                │
<nul set /p =!ESC![16;24H└────────────────────────────────┘
pause >nul & goto draw
:reset
set tab=1
set select=1
set what=0
goto :eof